plugins {
    id "com.jfrog.bintray" version '1.8.1'
}

ext {
    getScalaFull = [
        '2.11': '2.11.12',
        '2.12': '2.12.8',
        '2.13': '2.13.0-RC2'
    ]

    vScala = System.properties.scalaVersion ?: '2.12'
    vScalaFull = getScalaFull[vScala]
    vServletApi = '3.0.1'
    vMailApi = '1.6.2'
    vMockito = '1.10.19'
    vJUnit = '4.12'
}

tasks.withType(ScalaCompile) {
    def args = System.properties.compilerArgs ?: "-feature -deprecation -explaintypes -unchecked -Xlint"
    scalaCompileOptions.additionalParameters = args.split(" ").toList()
}
tasks.withType(Jar) {
    baseName = "${project.name}_$vScala"
}

if (version == '') {
    version = 'SNAPSHOT'
}
if (version == 'SNAPSHOT') {
    version = tsVersion(version)
}

def tsVersion(version) {
    java.text.SimpleDateFormat tsFmt = new java.text.SimpleDateFormat('yyMMddHHmm')
    tsFmt.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')))
    return "${version}-b${tsFmt.format(new Date())}"
}

wrapper {
    gradleVersion = '5.4.1'
}

allprojects {
    repositories {
        mavenCentral()
    }
    apply plugin: 'scala'
    apply plugin: 'maven-publish'
    apply plugin: 'eclipse'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    sourceSets {
        main {
            scala {
                if (vScala == '2.13') {
                    srcDirs = ['src/main/scala', 'src/2.13/main/scala', 'src/2.12+2.13/main/scala']
                } else if (vScala == '2.12') {
                    srcDirs = ['src/main/scala', 'src/2.12/main/scala', 'src/2.11+2.12/main/scala', 'src/2.12+2.13/main/scala']
                } else if (vScala == '2.11') {
                    srcDirs = ['src/main/scala', 'src/2.11/main/scala', 'src/2.11+2.12/main/scala']
                } else throw new IllegalArgumentException("Unsupported Scala version: " + vScala)
            }
        }
    }

    dependencies {

        compileOnly "org.scala-lang:scala-reflect:$vScalaFull"
        compileOnly "javax.servlet:javax.servlet-api:$vServletApi"

        compile "javax.mail:javax.mail-api:$vMailApi"
        compile "org.glassfish.main.external:jmxremote_optional-repackaged:4.1.2"

        testCompile "org.scala-lang:scala-reflect:$vScalaFull"
        testCompile "junit:junit:$vJUnit"
        testCompile "org.mockito:mockito-all:$vMockito"
        testCompile "javax.servlet:javax.servlet-api:$vServletApi"
    }

    jar {
        manifest {
            attributes "Implementation-Title": project.name, "Implementation-Version": version
        }
    }

    task sourceJar(type: Jar) {
        classifier "sources"
        from project.sourceSets.main.allScala
    }
    task docsJar(type: Jar, dependsOn: scaladoc) {
        classifier "docs"
        from project.scaladoc.destinationDir
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId System.properties.mavenGroupId ?: ""
                artifactId "${project.name}_$vScala"

                from components.java
                artifact sourceJar
                if (vScala != '2.13') { // Bug in 2.13.0-M4
                    artifact docsJar
                }
            }
        }
    }

    bintray {
        user = System.properties.bintrayUser ?: "dryrun"
        key = System.properties.bintrayKey ?: "dryrun"
        dryRun = (user == "dryrun" || key == "dryrun" || version.startsWith('SNAPSHOT'))
        publish = !dryRun
        override = true
        publications = ['maven']
        pkg {
            userOrg = System.properties.bintrayOrg ?: user
            repo = "maven"
            name = "Scuff"
        }
    }

}
